# Copyright 2017 The Clspv Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

if(NOT DEFINED SPIRV_GLSL_INPUT_FILE)
  message(FATAL_ERROR
    "Required cmake variable SPIRV_GLSL_INPUT_FILE not set!"
  )
endif()

if(NOT DEFINED SPIRV_GLSL_OUTPUT_FILE)
  message(FATAL_ERROR
    "Required cmake variable SPIRV_GLSL_OUTPUT_FILE not set!"
  )
endif()

if(NOT EXISTS ${SPIRV_GLSL_INPUT_FILE})
  message(FATAL_ERROR "File '${SPIRV_GLSL_INPUT_FILE}' does not exist!")
endif()

file(READ "${SPIRV_GLSL_INPUT_FILE}" contents)

string(REPLACE "\"instructions\" : [" "%" contents "${contents}")
string(REGEX REPLACE ".*%" "" contents "${contents}")
string(REPLACE "\"operands\" : [" "%" contents "${contents}")
string(REPLACE "\"capabilities\" : [" "%" contents "${contents}")
string(REPLACE "]" "&" contents "${contents}")
string(REGEX REPLACE "%[^&]*&" "" contents "${contents}")
string(REGEX REPLACE " *[},|{|,|&]" "" contents "${contents}")
string(REGEX REPLACE "[\r\n\t ]" "" contents "${contents}")

# We fork contents here into two variables because we need to generate an enum and a function.
string(REGEX REPLACE "\"opname\":\"([a-zA-Z0-9]*)\"\"opcode\":([0-9]*)" "  ExtInst\\1 = \\2,\n" enum "${contents}")

string(REGEX REPLACE "\"opname\":\"([a-zA-Z0-9]*)\"\"opcode\":([0-9]*)" "  case ExtInst\\1: return \"\\1\";\n" func "${contents}")

string(TOUPPER "${SPIRV_GLSL_OUTPUT_FILE}" header_ifndef)
string(REGEX REPLACE "[^A-Z]" "_" header_ifndef "${header_ifndef}")
set(header_ifndef "__${header_ifndef}__")

file(WRITE "${SPIRV_GLSL_OUTPUT_FILE}" "// Copyright 2017 The Clspv Authors. All rights reserved.\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "//\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "// Licensed under the Apache License, Version 2.0 (the \"License\");\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "// you may not use this file except in compliance with the License.\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "// You may obtain a copy of the License at\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "//\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "//     http://www.apache.org/licenses/LICENSE-2.0\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "//\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "// Unless required by applicable law or agreed to in writing, software\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "// distributed under the License is distributed on an \"AS IS\" BASIS,\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "// See the License for the specific language governing permissions and\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "// limitations under the License.\n\n")

file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "// THIS FILE IS AUTOGENERATED - DO NOT EDIT!\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "#ifndef ${header_ifndef}\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "#define ${header_ifndef}\n")

file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "namespace clspv {\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "namespace glsl {\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "enum ExtInst {\n")

file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "${enum}")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "  ExtInstMax = 0x7fffffffu\n")

file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "}; // enum ExtInst\n\n")

file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "const char* getExtInstName(const ExtInst thing) {\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "  switch(thing) {\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "${func}")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "  default: return \"\";\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "};\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "}\n")

file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "} // namespace glsl\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "} // namespace clspv\n")
file(APPEND "${SPIRV_GLSL_OUTPUT_FILE}" "#endif//${header_ifndef}\n\n")
